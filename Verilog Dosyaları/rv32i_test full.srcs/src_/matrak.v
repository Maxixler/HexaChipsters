// Matrak M10 RV32I RISC-V Processor
// G羹lpare II Architechture 2023
// Processor Module


module matrak (
   input                clk_i,
   input                rst_i,
   input                stall_i,       // 襤lemci durdurma sinyali
   input [31:0]         inst_i,        // Buyruk girii
   input [31:0]         data_i,        // Veri girii
   output               wen_o,         // Yazma yetkilendirme
   output               ren_o,         // Okuma yetkilendirme
   output [3:0]         stb_o,         // Bayt se癟im sinyali
   output [31:0]        inst_addr_o,   // Buyruk adresi
   output [31:0]        data_addr_o,   // Veri adresi
   output [31:0]        data_o         // Bellee yaz覺lacak veri
);

   // Getirme birimi balant覺lar覺
   wire c2f_pc_sel;
   wire [31:0] ac2f_pc_ext;
   wire [31:0] f2fd_pc_plus;

   fetch f1 (
      .clk_i(clk_i),
      .rst_i(rst_i),
      .stall_i(stall_i),
      .pc_sel_i(c2f_pc_sel),
      .pc_ext_i(ac2f_pc_ext),
      .pc_o(inst_addr_o),
      .pc_plus_o(f2fd_pc_plus)
   );

   // Boru hatt覺 kaydedicisi balant覺lar覺
   wire [31:0] fd2d_inst;
   wire [31:0] fd2ac_pc;
   wire [31:0] fd2w_pc_plus;
   wire c2fd_clear;

   // Boru hatt覺 temizleme sinyali
   wire clear = c2fd_clear | stall_i;

   fd_regs fd1 (
      .clk_i(clk_i),
      .rst_i(rst_i),
      .clear_i(clear),
      .inst_f_i(inst_i),
      .pc_f_i(inst_addr_o),
      .pc_plus_f_i(f2fd_pc_plus),
      .inst_d_o(fd2d_inst),
      .pc_d_o(fd2ac_pc),
      .pc_plus_d_o(fd2w_pc_plus)
   );

   // 繹zme mod羹l羹 balant覺lar覺
   wire c2d_regfile_wen;
   wire [2:0] c2d_imm_ext_sel;
   wire [31:0] w2d_result;
   wire [31:0] d2a_reg_a;
   wire [31:0] d2a_reg_b;
   wire [31:0] d2a_imm_ext;

   decode d1 (
      .clk_i(clk_i),
      .regfile_wen_i(c2d_regfile_wen),
      .imm_ext_sel_i(c2d_imm_ext_sel),
      .inst_i(fd2d_inst),
      .result_i(w2d_result),
      .reg_a_o(d2a_reg_a),
      .reg_b_o(d2a_reg_b),
      .imm_ext_o(d2a_imm_ext)
   );

   // ALU balant覺lar覺
   wire c2a_alu_sel;
   wire [3:0] c2a_alu_fun;
   wire [31:0] a2w_alu_out;
   wire a2c_alu_zero;

   alu a1 (
      .alu_sel_i(c2a_alu_sel),
      .alu_fun_i(c2a_alu_fun),
      .reg_a_i(d2a_reg_a),
      .reg_b_i(d2a_reg_b),
      .imm_ext_i(d2a_imm_ext),
      .alu_zero_o(a2c_alu_zero),
      .alu_out_o(a2w_alu_out)
   );

   // Adres hesaplay覺c覺 balant覺lar覺
   wire c2ac_ac_sel;

   address_calculator ac1(
      .ac_sel_i(c2ac_ac_sel),
      .pc_i(fd2ac_pc),
      .imm_ext_i(d2a_imm_ext),
      .reg_a_i(d2a_reg_a),
      .pc_ext_o(ac2f_pc_ext)
   );

   // Geriyazma birimi balant覺lar覺
   wire [2:0] c2w_result_sel;
   wire [31:0] ls2w_rdata;

   writeback w1 (
      .result_sel_i(c2w_result_sel),
      .alu_out_i(a2w_alu_out),
      .pc_plus_i(fd2w_pc_plus),
      .imm_ext_i(d2a_imm_ext),
      .pc_ext_i(ac2f_pc_ext),
      .ls_rdata_i(ls2w_rdata),
      .result_o(w2d_result)
   );

   // Y羹kleme depolama birimi balant覺lar覺
   wire c2ls_wen;
   wire c2ls_ren;
   wire [2:0] c2ls_fmt;

   load_store ls1 (
      .ls_wen_i(c2ls_wen),
      .ls_ren_i(c2ls_ren),
      .ls_fmt_i(c2ls_fmt),
      .ls_addr_i(a2w_alu_out),
      .ls_wdata_i(d2a_reg_b),
      .ls_rdata_i(data_i),
      .ls_wen_o(wen_o),
      .ls_ren_o(ren_o),
      .ls_stb_o(stb_o),
      .ls_addr_o(data_addr_o),
      .ls_wdata_o(data_o),
      .ls_rdata_o(ls2w_rdata)
   );

   controller c1 (
      .inst_i(fd2d_inst),
      .alu_zero_i(a2c_alu_zero),
      .regfile_wen_o(c2d_regfile_wen),
      .imm_ext_sel_o(c2d_imm_ext_sel),
      .alu_sel_o(c2a_alu_sel),
      .alu_fun_o(c2a_alu_fun),
      .pc_sel_o(c2f_pc_sel),
      .ac_sel_o(c2ac_ac_sel),
      .result_sel_o(c2w_result_sel),
      .ls_wen_o(c2ls_wen),
      .ls_ren_o(c2ls_ren),
      .ls_fmt_o(c2ls_fmt),
      .clear_o(c2fd_clear)
   );

endmodule

module fetch (
   input                clk_i,
   input                rst_i,
   input                stall_i,    // Program sayac覺 durdurma girii
   input                pc_sel_i,   // Program sayac覺 se癟im girii
   input [31:0]         pc_ext_i,   // Dallanma adres girii
   output reg [31:0]    pc_o,       // Program sayac覺 癟覺k覺覺
   output [31:0]        pc_plus_o   // Program sayac覺 + 4 癟覺k覺覺
);

   // Program sayac覺na 4 ekle
   assign pc_plus_o = pc_o + 4;

   // Dallanma adresi veya PC + 4 
   wire [31:0] pc_next = pc_sel_i ? pc_ext_i : pc_plus_o;

   always @(posedge clk_i, posedge rst_i) begin
      if (rst_i) begin
         pc_o <= 32'h0000_0000;
      end else begin
         if (!stall_i) begin // Durdurma istei yoksa PC'yi g羹ncelle
            pc_o <= pc_next;
         end
      end
   end

endmodule

module fd_regs (
   input                clk_i,
   input                rst_i,
   input                clear_i,     // S覺f覺rlama sinyali (boru hatt覺 boaltma)
   input [31:0]         inst_f_i,    // Buyruk girii (bellekten geliyor)
   input [31:0]         pc_f_i,      // Progam sayac覺 girii (getirme biriminden geliyor)
   input [31:0]         pc_plus_f_i, // Program sayac覺 + 4 girii (getirme biriminden geliyor)
   output reg [31:0]    inst_d_o,    // Buyruk 癟覺k覺覺 (y羹r羹tme aamas覺na gidiyor)
   output reg [31:0]    pc_d_o,      // Program sayac覺 癟覺k覺覺 (y羹r羹tme aamas覺na gidiyor)
   output reg [31:0]    pc_plus_d_o  // Program sayac覺 + 4 癟覺k覺覺 (y羹r羹tme aamas覺na gidiyor)
);

   always @(posedge clk_i, posedge rst_i) begin
      if (rst_i) begin
         inst_d_o    <= 32'b0;
         pc_d_o      <= 32'b0;
         pc_plus_d_o <= 32'b0;
      end else begin
         if (clear_i) begin // Boru hatt覺 boalt覺l覺yor.
            inst_d_o    <= 32'b0;
            pc_d_o      <= 32'b0;
            pc_plus_d_o <= 32'b0;
         end else begin
            inst_d_o    <= inst_f_i;
            pc_d_o      <= pc_f_i;
            pc_plus_d_o <= pc_plus_f_i;
         end
      end
   end

endmodule

module decode (
   input                   clk_i,
   input                   regfile_wen_i, // Kaydedici dosyas覺 yazma yetkilendirme
   input [2:0]             imm_ext_sel_i, // 襤vedi geniletici format se癟imi
   input [31:0]            inst_i,        // Boru hatt覺 kaydedicisinden gelen buyruk
   input [31:0]            result_i,      // Hedef kaydedicisine (rd) yaz覺lacak deer 
   output [31:0]           reg_a_o,       // Birinci kaynak kaydedicisinin (rs1) deeri
   output [31:0]           reg_b_o,       // 襤kinci kaynak kaydedicisinin (rs2) deeri
   output reg [31:0]       imm_ext_o      // 襤vedi genileticinin 癟覺k覺覺
);

   // 32 bit genilikte 32 adet kaydedicili kaydedici dosyas覺
   reg [31:0] regfile [31:0];

   // Kaydedici adreslerini buyruktan ay覺kla
   wire [4:0] reg_a_addr      = inst_i[19:15];  // rs1 adres
   wire [4:0] reg_b_addr      = inst_i[24:20];  // rs2 adres
   wire [4:0] target_reg_addr = inst_i[11:7];   // rd adres

   // Kaydedici dosyas覺ndan oku
   assign reg_a_o = (reg_a_addr == 5'b0) ? 32'b0 : regfile[reg_a_addr]; // rs1 deeri
   assign reg_b_o = (reg_b_addr == 5'b0) ? 32'b0 : regfile[reg_b_addr]; // rs2 deeri

   // Kaydedici dosyas覺na yaz
   always @(posedge clk_i) begin
      if (regfile_wen_i) begin
         regfile[target_reg_addr] <= result_i;
      end
   end

   // 襤vedi geniletici
   always @(*) begin
      case (imm_ext_sel_i)
         3'b000   : imm_ext_o = {{20{inst_i[31]}}, inst_i[31:20]}; // I-type
         3'b001   : imm_ext_o = {{20{inst_i[31]}}, inst_i[7], inst_i[30:25], inst_i[11:8], 1'b0}; // B-type
         3'b010   : imm_ext_o = {{12{inst_i[31]}}, inst_i[19:12], inst_i[20], inst_i[30:21], 1'b0}; // J-type
         3'b011   : imm_ext_o = {inst_i[31:12], 12'b0}; // U-type
         3'b100   : imm_ext_o = {{20{inst_i[31]}}, inst_i[31:25], inst_i[11:7]}; // S-type
         default  : imm_ext_o = 32'b0; 
      endcase
   end

endmodule

module alu (
   input                      alu_sel_i,  // 襤kinci ilenenin se癟im sinyali (rs2 veya imm)
   input [3:0]                alu_fun_i,  // 襤lem se癟im sinyali
   input [31:0]               reg_a_i,    // rs1 deeri
   input [31:0]               reg_b_i,    // rs2 deeri
   input [31:0]               imm_ext_i,  // imm deeri
   output                     alu_zero_o, // Sonu癟 s覺f覺r sinyali
   output reg [31:0]          alu_out_o   // Sonu癟 deeri
);

   // Birinci ilenen iki buyruk format覺nda da sabit.
   wire signed [31:0] alu_a = reg_a_i;
   // 襤kinci ilenen se癟im sinyaline g繹re belirleniyor.
   wire signed [31:0] alu_b = alu_sel_i ? imm_ext_i : reg_b_i;

   // Sonu癟 0'a eit ise alu_zero_o sinyali 1 olur.
   assign alu_zero_o = ~(|alu_out_o);

   always @(*) begin
      case (alu_fun_i)
         4'b0000  : alu_out_o = alu_a + alu_b;           // Toplama 
         4'b0001  : alu_out_o = alu_a - alu_b;           // 覺karma
         4'b0010  : alu_out_o = alu_a & alu_b;           // VE
         4'b0011  : alu_out_o = alu_a ^ alu_b;           // XOR
         4'b0100  : alu_out_o = alu_a | alu_b;           // VEYA
         4'b0101  : alu_out_o = alu_a << alu_b[4:0];     // Sola kayd覺rma
         4'b0110  : alu_out_o = alu_a >> alu_b[4:0];     // Saa kayd覺rma
         4'b0111  : alu_out_o = alu_a >>> alu_b[4:0];    // Aritmetik saa kayd覺rma
         4'b1000  : alu_out_o = {31'b0, alu_a == alu_b}; // Eitse alu_out_o = 1, deilse alu_out_o = 0 (beq, bne)
         4'b1001  : alu_out_o = {31'b0, alu_a < alu_b};  // K羹癟羹kse alu_out_o = 1, deilse alu_out_o = 0 (blt, bge, slt, slti)
         4'b1010  : alu_out_o = {31'b0, $unsigned(alu_a) < $unsigned(alu_b)}; // (襤aretsiz) k羹癟羹kse alu_out_o = 1, deilse alu_out_o = 0 (bltu, bgeu, sltu, sltiu)
         //m komut seti 
         4'b1011  : alu_out_o = alu_a * alu_b;           // ドrpma (M komut seti mul)
         4'b1100  : alu_out_o = alu_a / alu_b;           // B闤me (M komut seti div)
         4'b1101  : alu_out_o = alu_a % alu_b;           // B闤mden kalan (M komut seti rem)
         
         

         default  : alu_out_o = 32'bx;                   // Ge癟ersiz alu_fun_i sinyali
      endcase
   end

endmodule

module address_calculator (
   input                      ac_sel_i,   // Kontrol biriminden gelen kaynak se癟im sinyali
   input [31:0]               pc_i,       // Boru hatt覺 kaydedicisinden gelen program sayac覺n覺n deeri
   input [31:0]               imm_ext_i,  // 繹zme biriminden gelen ivedi deer
   input [31:0]               reg_a_i,    // 繹zme biriminden gelen rs1 deeri
   output [31:0]              pc_ext_o    // Program sayac覺na yaz覺lacak adres
);

   wire [31:0] operand = ac_sel_i ? reg_a_i : pc_i;

   assign pc_ext_o = operand + imm_ext_i;

endmodule

module writeback (
   input [2:0]                result_sel_i,  // Kontrol biriminden gelen se癟im sinyali
   input [31:0]               alu_out_i,     // ALU sonucu
   input [31:0]               pc_plus_i,     // Program sayac覺 + 4
   input [31:0]               imm_ext_i,     // 襤vedi deer
   input [31:0]               pc_ext_i,      // Adres hesaplay覺c覺dan gelen adres
   input [31:0]               ls_rdata_i,    // Bellekten okunan deer
   output reg [31:0]          result_o       // Kaydedici dosyas覺na yaz覺lacak deer
);

   always @(*) begin
      case (result_sel_i)
         3'b000   : result_o = alu_out_i;
         3'b001   : result_o = pc_plus_i;
         3'b010   : result_o = imm_ext_i;
         3'b011   : result_o = pc_ext_i;
         3'b100   : result_o = ls_rdata_i;
         default  : result_o = 32'bx;
      endcase
   end

endmodule

module load_store (
   input                      ls_wen_i,   // Yazma yetkilendirme, kontrol biriminden geliyor.
   input                      ls_ren_i,   // Okuma yetkilendirme, kontrol biriminden geliyor.
   input [2:0]                ls_fmt_i,   // funct3 deeri, kontrol biriminden geliyor.
   input [31:0]               ls_addr_i,  // Adres girii, ALU'dan geliyor.
   input [31:0]               ls_wdata_i, // rs2 deeri, 癟繹zme biriminden geliyor.
   input [31:0]               ls_rdata_i, // Bellekten okunan deer.
   output                     ls_wen_o,   // Yazma yetkilendirme, bellee gidiyor.
   output                     ls_ren_o,   // Okuma yetkilendirme, bellee gidiyor.
   output reg [3:0]           ls_stb_o,   // Bayt se癟im sinyali, bellee gidiyor.
   output [31:0]              ls_addr_o,  // Adres 癟覺k覺覺, bellee gidiyor.
   output [31:0]              ls_wdata_o, // Yaz覺lacak veri, bellee gidiyor.
   output reg [31:0]          ls_rdata_o  // Okunan veri, geriyazma birimine gidiyor.
);

   // Bu sinyaller dorudan bellee gidiyor.
   assign ls_addr_o  = ls_addr_i;
   assign ls_wen_o   = ls_wen_i;
   assign ls_ren_o   = ls_ren_i;

   // Kayd覺r覺lacak deer hesaplan覺yor.
   wire [4:0] shift_value = ls_addr_i[1:0] << 5'd3;

   // Bellee yaz覺lacak veri hizalan覺yor.
   assign ls_wdata_o = ls_wdata_i << shift_value;

   // Bellekten okunan veri hizalan覺yor.
   wire [31:0] aligned_data = ls_rdata_i >> shift_value;

   // Bellekten okunan hizalanm覺 veri geniletiliyor.
   always @(*) begin
      case (ls_fmt_i[1:0])
         2'b00    : ls_rdata_o = {{24{~ls_fmt_i[2] & aligned_data[7]}}, aligned_data[7:0]};     // lb, lbu
         2'b01    : ls_rdata_o = {{16{~ls_fmt_i[2] & aligned_data[15]}}, aligned_data[15:0]};   // lh, lhu
         2'b10    : ls_rdata_o = aligned_data[31:0];  // lw
         default  : ls_rdata_o = 32'bx;
      endcase
   end

   // Bayt se癟im sinyali ayarlan覺yor.
   always @(*) begin
      case (ls_fmt_i[1:0])
         2'b00    : ls_stb_o = 4'b0001 << ls_addr_i[1:0];   // sb
         2'b01    : ls_stb_o = 4'b0011 << ls_addr_i[1:0];   // sh
         2'b10    : ls_stb_o = 4'b1111 << ls_addr_i[1:0];   // sw
         default  : ls_stb_o = 4'b0;
      endcase
   end

endmodule

module controller (
   input [31:0]               inst_i,        // Boru hatt覺 kaydedicisinden gelen buyruk
   input                      alu_zero_i,    // ALU'dan gelen sonu癟 s覺f覺r sinyali
   output                     regfile_wen_o, // Kaydedici dosyas覺 yazma yetkilendirme sinyali
   output [2:0]               imm_ext_sel_o, // 襤vedi geniletici format se癟im sinyali
   output                     alu_sel_o,     // ALU ikinci ilenen se癟im sinyali
   output reg [3:0]           alu_fun_o,     // ALU ilem se癟im sinyali
   output                     pc_sel_o,      // Program sayac覺 adres se癟im sinyali
   output                     ac_sel_o,      // Adres hesaplay覺c覺 kaynak se癟im sinyali
   output [2:0]               result_sel_o,  // Geriyazma kaynak se癟im sinyali
   output                     ls_wen_o,      // Bellek yazma yetkilendirme sinyali
   output                     ls_ren_o,      // Bellek okuma yetkilendirme sinyali
   output [2:0]               ls_fmt_o,      // Y羹kleme depolama birimi i癟in funct3 sinyali
   output                     clear_o        // Boru hatt覺 boaltma sinyali
);

   // Buyruun gerekli b繹l羹mleri ay覺klan覺yor.
   wire [6:0] opcode = inst_i[6:0];
   wire [2:0] funct3 = inst_i[14:12];
   wire [6:0] funct7 = inst_i[31:25];

   assign ls_fmt_o = funct3;

   wire [1:0] alu_dec;
   wire branch_op;
   wire jump_op;

   reg [14:0] control_signals;
   assign {regfile_wen_o, imm_ext_sel_o, alu_sel_o, alu_dec, branch_op, jump_op, ac_sel_o, result_sel_o, ls_wen_o, ls_ren_o} = control_signals;

   always @(*) begin
      case (opcode)
         7'b0110011  : control_signals = 15'b1_xxx_0_11_0_0_0_000_0_0; // R-type buyruk
         7'b0010011  : control_signals = 15'b1_000_1_11_0_0_0_000_0_0; // I-type buyruk
         7'b1100011  : control_signals = 15'b0_001_0_01_1_0_0_000_0_0; // B-type buyruk
         7'b1101111  : control_signals = 15'b1_010_0_00_0_1_0_001_0_0; // jal
         7'b1100111  : control_signals = 15'b1_000_0_00_0_1_1_001_0_0; // jalr
         7'b0110111  : control_signals = 15'b1_011_0_00_0_0_0_010_0_0; // lui
         7'b0010111  : control_signals = 15'b1_011_0_00_0_0_0_011_0_0; // auipc
         7'b0000011  : control_signals = 15'b1_000_1_10_0_0_0_100_0_1; // load buyruklar覺
         7'b0100011  : control_signals = 15'b0_100_1_10_0_0_0_000_1_0; // store buyruklar覺
         7'b0000000  : control_signals = 15'b0_000_0_00_0_0_0_000_0_0; // S覺f覺rlama durumu
         default     : control_signals = 15'bx_xxx_x_xx_x_x_x_xxx_x_x; // Ge癟ersiz buyruk
      endcase
   end

   // Buyruk R-type ise ve funct7 deeri 0x20 ise 癟覺karma ilemi anlam覺na gelir.
   wire sub = opcode[5] & funct7[5];

   // ALU'da yap覺lacak ilem belirleniyor.
   always @(*) begin
      case (alu_dec)
         2'b01    : // B-type
            case (funct3)
               3'b000   : alu_fun_o = 4'b1000; // beq
               3'b001   : alu_fun_o = 4'b1000; // bne
               3'b100   : alu_fun_o = 4'b1001; // blt
               3'b101   : alu_fun_o = 4'b1001; // bge
               3'b110   : alu_fun_o = 4'b1010; // bltu
               3'b111   : alu_fun_o = 4'b1010; // bgeu
               default  : alu_fun_o = 4'bx;
            endcase
         2'b11    : // R-type veya I-type
            case (funct3)
               3'b000   : // add-addi veya sub buyruu
                  if (sub) begin
                     alu_fun_o = 4'b0001; // sub
                  end else begin
                     alu_fun_o = 4'b0000; // add, addi
                  end
               3'b001   : alu_fun_o = 4'b0101; // sll, slli
               3'b010   : alu_fun_o = 4'b1001; // slt, slti
               3'b011   : alu_fun_o = 4'b1010; // sltu, sltiu
               3'b100   : alu_fun_o = 4'b0011; // xor, xori
               3'b101   : // srl, srli, sra, srai
                  if (funct7[5]) begin
                     alu_fun_o = 4'b0111; // sra, srai
                  end else begin
                     alu_fun_o = 4'b0110; // srl, srli
                  end
               3'b110   : alu_fun_o = 4'b0100; // or, ori
               3'b111   : alu_fun_o = 4'b0010; // and, andi
               default  : alu_fun_o = 4'b0000;
            endcase
         default  : alu_fun_o = 4'b0000; // Varsay覺lan ilem toplama
      endcase
   end

   reg branch_valid;

   always @(*) begin
      case (funct3)
         3'b000   : branch_valid = !alu_zero_i;   // beq
         3'b001   : branch_valid = alu_zero_i;    // bne
         3'b100   : branch_valid = !alu_zero_i;   // blt
         3'b101   : branch_valid = alu_zero_i;    // bge
         3'b110   : branch_valid = !alu_zero_i;   // bltu
         3'b111   : branch_valid = alu_zero_i;    // bgeu
         default  : branch_valid = 1'b0;
      endcase
   end

   assign pc_sel_o   = (branch_op & branch_valid) | jump_op; // Dallanma ve atlama durumu kontrol ediliyor.
   assign clear_o    = pc_sel_o; // Boru hatt覺n覺 boalt

endmodule
